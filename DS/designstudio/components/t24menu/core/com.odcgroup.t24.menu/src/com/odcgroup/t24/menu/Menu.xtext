grammar com.odcgroup.t24.menu.Menu /*with com.odcgroup.workbench.dsl.Common hidden(ID, WS, ML_COMMENT, SL_COMMENT)*/
//with org.eclipse.xtext.common.Terminals 
hidden(WS, ML_COMMENT, SL_COMMENT)
  
import "http://www.eclipse.org/emf/2002/Ecore" as ecore 

import "platform:/resource/com.odcgroup.mdf.ecore/src/main/model/mdf.ecore" as mdf

import "http://www.odcgroup.com/t24/version/VersionDSL" as version

import "http://www.odcgroup.com/t24/Enquiry" as enquiry

import "http://www.temenos.com/DS/t24ui" as t24ui
 
generate menu "http://www.odcgroup.com/t24/menu/Menu"

MenuRoot returns MenuRoot:
	'metamodelVersion' '=' metamodelVersion=STRING 
	rootItem = MenuItem
;

MenuItem returns MenuItem:
	name=VALUE
	('enabled' '=' enabled=Enabled)? 
	('displayTabs' '=' displayTabs?='true')? 
	('securityRole' '='securityRole=STRING)?
    ('shortcut' '='shortcut=STRING)?
	('labels' labels+=Translation ( "," labels+=Translation)*)?
	( 
	    ('version' '=' version = [version::Version|VALUE])
	  | ('enquiry' '=' enquiry = [enquiry::Enquiry|VALUE])
	  | ('composite-screen' '=' compositeScreen = [t24ui::CompositeScreen|VALUE])
	  | ('include-menu' '=' includedMenu = [MenuRoot|VALUE])
	  | ('application' '=' application = [mdf::MdfClass|VALUE])
	)?
	(  
      // optional additional free parameters for version, enquiry, cos or included menu
	  // the content is interpreted by the generator.
      'parameters' '=' parameters=STRING
    )?
	('{' 
		(submenus+=MenuItem (submenus+=MenuItem)*)?
    '}')?
;
	
Translation:
	language=VALUE '=' message=STRING
;

enum Enabled:
	true | false | conditional
;

terminal STRING  : '"' ( '\\'('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|'"') )* '"';
terminal VALUE : ('a'..'z'|'A'..'Z'|'0'..'9'|'_'|'&'|'/'|'%'|'.'|','|'-'|'\u00e9'|'\u00e8'|'\u00e0'|'\u00e4'|'\u00f6'|'\u00fc'|'\u00c4'|'\u00d6'|'\u00dc'|'\u00c9'|'\u00c8'|'\u00c0'|'\u00e7'|'\u00df')+;

terminal ML_COMMENT	: '/*' -> '*/';
terminal SL_COMMENT : '#' !('\n'|'\r')* ('\r'? '\n')?;
terminal WS			: (' '|'\t'|'\r'|'\n')+;



